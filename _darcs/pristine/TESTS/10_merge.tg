/* Et voici un exemple de programme plus complexe: 
   le merge de deux listes lues sur l'entree standard */

type any = {any : int} in 
type list = {first: int, rest: list} in
var buffer := 'x' in
function readlist() : list =
  function readaint(any: any) : int =
    var i := 0 in 
    function isdigit(s : char) : int = 
        if ord(buffer)>=ord('0') & ord(buffer)<=ord('9') then 1 else 0
    and skipto() =
          while buffer=' ' | buffer='\n'
            do buffer := getchar() done
    in skipto();
       any.any := isdigit(buffer);
       while (isdigit(buffer)=1)
         do i := i*10+ord(buffer)-ord('0'); buffer := getchar() done;
       i
  in
  var any := any{any=0} in
  var i := readaint(any) in
  if any.any > 0
  then list{first=i,rest=readlist()}
  else nil
in
function merge(a: list, b: list) : list =
   if a=nil then b
   else if b=nil then a
   else if a.first < b.first 
      then list{first=a.first,rest=merge(a.rest,b)}
      else list{first=b.first,rest=merge(a,b.rest)}
in
function printlist(l: list) =
   function printint(i: int) =
     function f(i:int) = if i>0 
	            then (f(i/10); print(mkstring(chr(i-i/10*10+ord('0')))))
     in 
     if i<0 then (print("-"); f(-i))
            else if i>0 then f(i)
                        else print("0")
     in
     if l=nil then print("\n")
     else (printint(l.first); print(" "); printlist(l.rest))
in
print("Entrez une liste triee d'entiers positifs, terminez par un caractere qui n'est ni chiffre ni espace\n");
buffer:=getchar();
var list1 := readlist() in
var list2 := (
  print("Entrez une liste triee d'entiers positifs, terminez par un caractere qui n'est ni chiffre ni espace\n");
  buffer:=getchar(); readlist())
in

/* BODY OF MAIN PROGRAM */
var merged:=merge(list1,list2) in
print("Voici la fusion des deux listes en entree:\n");
printlist(merged)
